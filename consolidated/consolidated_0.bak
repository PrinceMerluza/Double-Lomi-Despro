/* 
Consolidated versions of PB3 and lcd control
candidate for final
*/
/*
------------ BACKUP NOTE ----------------
9/13
This is the latest working iteration before the 9/13 phonebook3.ino update

Includes SIM900 phonebook functions of adding/delting numbe
DOES NOT include send/receive of message.

*/


#include <Keypad.h>
#include <SoftwareSerial.h>
#include <Wire.h>
#include <SoftwareSerial.h>
#include <stdio.h>
#include "rgb_lcd.h"

#define MAINMENUOPTIONS 3
#define MAXCONTACTS 15
#define TEXTMESSAGEMAX 30

SoftwareSerial SIM900(10, 11);

byte index = 0; //index for filling serial input array
byte iForPBList = 1; //index for phonebook list
char inChar = -1; //temp char for incoming char for serial input
byte state = 0;
const char* TRUSTKEY = "DoMoLuMe"; 

byte numContacts = 0;

char input[20] = ""; //serial monitor input
char* ptr_input = input; //pointer for serial input

char ATcommand[50] = ""; //commands to be sent to SIM900
char tempNum[12] = ""; //phone number parameter

//first 11 digits of cstring is celphone number. 12th is a letter [a-z] for indexing and ID.
//this makes the technical limit of cp numbers up to 26.
char pbNumberList[MAXCONTACTS][13];

//timing and display
unsigned long mill = 0;
unsigned long prevDisp = 0;
int displayRefresh = 100;
rgb_lcd lcd;

//Keypad setup
const byte ROWS = 4; //four rows
const byte COLS = 4; //three columns
char keys[ROWS][COLS] = {
  {'1','2','3','A'},
  {'4','5','6','B'},
  {'7','8','9','C'},
  {'*','0','#','D'}
};
byte rowPins[ROWS] = {31, 33, 35, 37}; //connect to the row pinouts of the keypad
byte colPins[COLS] = {23, 25, 27, 29}; //connect to the column pinouts of the keypad

Keypad keypad = Keypad( makeKeymap(keys), rowPins, colPins, ROWS, COLS );

//MAIN MENU SETUP
const char lcdMainMenu[MAINMENUOPTIONS][17] = {
	"1.Add Contact",
	"2.Del Contact",
	"3.Lst Contacts"
}; 
byte menuState = 0;
//states 0 -main menu.
//1: adding a contact number
//2: delete a number
//3: List Contacts
//4: Messgae

//variable for menu display and position
byte maxPage = 0;
byte curPage = 0;
byte curPos = -1;
byte curOption = 0;

//variables for phone number input
char inputPhoneNum[14] = "";

//vars for message display
byte prevState = 0;
char messageForDisplay[17] = "NaN";

byte cursor[8] = {
    0b10000,
    0b11000,
    0b11100,
    0b11110,
    0b11110,
    0b11100,
    0b11000,
    0b10000
};

void setup(){
	//serial setup
	Serial.begin(19200); // for serial monitor
	while(!Serial);
	SIM900.begin(19200); // for GSM shield
	while(!SIM900);
	delay(100);
	
	countContacts();
	
	//lcd setup
	lcd.begin(16, 2);
	lcd.setRGB(0,255,255);
	lcd.createChar(1, cursor);
	
	callMainMenu();
	delay(1000); 
}

void countContacts(){
	numContacts = 0;
	clearNumberList();
	for(byte i = 1; i < MAXCONTACTS; i++){
		strcpy(ATcommand, "AT+CPBR=");
		addBytToString(ATcommand, i);
		SIM900.println(ATcommand);
		delay(100);
		
		while(SIM900.available() > 0){
			inChar = SIM900.read(); 
			Serial.print(inChar);
			if(inChar == ':'){
				while(inChar != '"'){
					inChar = SIM900.read();
				}
				for(byte ii = 0; ii < 11; ii++){
					tempNum[ii] = SIM900.read();
				}
				strcat(pbNumberList[numContacts],tempNum);
				pbNumberList[numContacts][11] = (char)(i + 96); //put the char ID				
				pbNumberList[numContacts][12] = '\0';
				
				numContacts++;
			}
		}
	}
	Serial.print("Number of Contacts: ");
	Serial.println(numContacts);	
}

//clear the numbers list form the array bNumberList
void clearNumberList(){
	for(byte i = 0; i < MAXCONTACTS; i++){
		pbNumberList[i][0] = '\0';
	}
}

//switch menu to the main menu
void callMainMenu(){
	curPos = 0;
	curOption = 0;
	curPage = 0;
	maxPage = (MAINMENUOPTIONS / 2) + 1;
	menuState = 0;
}

//concatenate a single char to a cstring
void addCharToString(char* srcStr, char a){
    char* tmpstr;
    tmpstr = (char *) malloc(2);
    tmpstr[0] = a;
    tmpstr[1] = '\0';
 
    strcat(srcStr, tmpstr);
    free(tmpstr);
}

//concatenate a byte to a cstring
void addBytToString(char* srcStr, byte a){
	if(a < 10){
		addCharToString(srcStr, char(a + 48));	
	}else{
		addCharToString(srcStr, char((a/10) + 48));
		addCharToString(srcStr, char((a%10) + 48));
	}
}

void showMessage(char* msg){
	messageForDisplay[0] = '\0';
	strcat(messageForDisplay, msg);
	prevState = menuState;
	menuState = 4;
}

//reset vlaues after every state change
void resetStuff(){
	ATcommand[0] = '\0';
	iForPBList = 1;
	state = 0;
}

//when a key from keypad is pressed
void keyPressed(char k){
	Serial.println(k);
	switch(menuState){
	case 0:
	//MAIN MENU INPUT
		if(k == 'D'){ //DOWN button
			curOption++;
			if(curOption >= MAINMENUOPTIONS) curOption = 0;
		}
		if(k == 'C'){ // UP button
			curOption--;
			if(curOption == 255) curOption = MAINMENUOPTIONS - 1;
		}
		if(k == 'A'){ //choose option
			switch(curOption){
			case 0: menuState = 1; break;
			case 1: 
				menuState = 2; 
				curPos = 0;
				curOption = 0;
				curPage = 0;
				maxPage = (numContacts / 2) + 1;
				break;
			case 2: 
				menuState = 3;
				curPos = 0;
				curOption = 0;
				curPage = 0;
				maxPage = (numContacts / 2) + 1;	
				break;
			}
		}
		curPage = curOption / 2;
		curPos = curOption % 2;
		break;
	
	case 1:
	//ADD CONTACT INPUT
		if((k >= 48) && (k <= 57)) //if key pressed is numbers 0-9
		addCharToString(inputPhoneNum, k);	
		
		if(k == 'B'){ //erase digit
			if(strlen(inputPhoneNum) > 0)
				inputPhoneNum[strlen(inputPhoneNum)-1] = '\0';
			else
				menuState = 0;
		}
		if(k == 'D'){ //erase everything
			inputPhoneNum[0] = '\0';
		}
		if(k == 'A'){ //accept number
			//input validation
			if((strlen(inputPhoneNum) != 11) || 
			  (inputPhoneNum[0] != '0') ||
			  (inputPhoneNum[1] != '9')){
				showMessage("Invalid number");
			}else{
				//successful input

				strcpy(ATcommand, "AT+CPBW=,\"");
				strcat(ATcommand, inputPhoneNum);
				strcat(ATcommand, "\",129,\"");
				strcat(ATcommand, TRUSTKEY);
				strcat(ATcommand, "\"\r");

				SIM900.print(ATcommand);
				delay(100);
				
				//cleanup
				inputPhoneNum[0] = '\0';
				menuState = 0;
				showMessage("Number added");
				
				countContacts();
			}
		}
		break;
	case 2:
		if(k == 'D'){ //DOWN button
			curOption++;
			if(curOption >= numContacts) curOption = 0;
		}
		if(k == 'C'){ // UP button
			curOption--;
			if(curOption == 255) curOption = numContacts - 1;
		}
		if(k == 'B'){ // BACK button to main menu
			callMainMenu();
		}
		if(k == 'A'){ //delete
			strcpy(ATcommand, "AT+CPBW=");
			addBytToString(ATcommand, (byte)(pbNumberList[curOption][11] - 96));
			strcat(ATcommand, "\r");

			SIM900.print(ATcommand);
			delay(100);
			
			showMessage("Deleted Number");
			countContacts();
		}
		
		
		curPage = curOption / 2;
		curPos = curOption % 2;
		break;
	//LIST CONTACT CONTROLS
	case 3:
		if(k == 'D'){ //DOWN button
			curOption++;
			if(curOption >= numContacts) curOption = 0;
		}
		if(k == 'C'){ // UP button
			curOption--;
			if(curOption == 255) curOption = numContacts - 1;
		}
		if(k == 'B'){ // BACK button to main menu
			callMainMenu();
		}
		
		curPage = curOption / 2;
		curPos = curOption % 2;
		break;
	case 4:
		if(k == 'C')
			menuState = prevState;
		break;
	}
}

//display for the screen.
void screenDisplay(){
	lcd.clear();
	
	switch(menuState){
	//MAIN MENU DISPLAY
	case 0:
		//screen cursor position
		lcd.setCursor(0, curPos);
		lcd.write(1);
	
		//main menu contents
		lcd.setCursor(1, 0);
		lcd.print(lcdMainMenu[0 + (curPage * 2)]);
		if((curPage != maxPage - 1) || (MAINMENUOPTIONS % 2 != 0)){
			lcd.setCursor(1, 1);
			lcd.print(lcdMainMenu[1 + (curPage * 2)]);
		}
		break;
	//ADD CONTACT DISPLAY
	case 1:
		lcd.setCursor(0, 0);
		lcd.print("Enter number:");
		lcd.setCursor(0, 1);
		lcd.print(inputPhoneNum);
		lcd.setCursor(strlen(inputPhoneNum), 1);
		lcd.print("_");
		break;
	//DEL CONTACTACT DISPLAY
	case 2:
	//LIST CONTACT DISPLAY
	case 3:
		//screen cursor position
		lcd.setCursor(0, curPos);
		lcd.write(1);
	
		//phonue number contents
		lcd.setCursor(1, 0);
		lcd.print(pbNumberList[0 + (curPage * 2)]);
		if((curPage != maxPage - 1) || (numContacts % 2 != 0)){
			lcd.setCursor(1, 1);
			lcd.print(pbNumberList[1 + (curPage * 2)]);
		}
		lcd.setCursor(12,0);
		lcd.print(" ");
		lcd.setCursor(12,1);
		lcd.print(" ");
		break;
	case 4:
		lcd.setCursor(0,0);
		lcd.print(messageForDisplay);
		lcd.setCursor(0, 1);
		lcd.print("press [C]ontinue");
		break;
	}
}

void loop(){
	char key = keypad.getKey();
	mill = millis();

	//check for keypad input
	if (key){
		keyPressed(key);
	}
	
	//refresh rate for lcd
	if((unsigned long)(mill - prevDisp) > displayRefresh){
		screenDisplay();
		prevDisp = mill;
	}
}